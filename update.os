Перем Пользователь;
Перем ПутьКФайлуReadme;
Перем КоличествоСтатей;
Перем Фильтр;
Перем Шаблон;
Перем МаксимальноеКоличествоПубликаций;

Процедура ОсновнаяОбработка()

	МассивАргументов = АргументыКоманднойСтроки;
	Пользователь     = МассивАргументов[0];
	ПутьКФайлуReadme = МассивАргументов[1];
	КоличествоСтатей = МассивАргументов[2];
	Фильтр           = МассивАргументов[3];
	Шаблон           = МассивАргументов[4];

	МаксимальноеКоличествоПубликаций = 10;

	Если Не ЗначениеЗаполнено(СокрЛП(Шаблон)) Тогда
		Шаблон = 
			"
			|> <img src=""%1"" width=""96"" align=""left""> 
			|> <h4 style=""color: white;""><a href=""%3"">%2</a></h4>
			|> <small>%5</small>  
			|> <br clear=""left"">
			|>
			|> | :star: %4 |  :calendar: %6 |  :speech_balloon: %7 |  :eyes: %8 |
			|>  |-|-|-|-|  ";
	Иначе
		ТекстовыйДокумент = Новый ТекстовыйДокумент();
		ТекстовыйДокумент.Прочитать(Шаблон);
		Шаблон = ТекстовыйДокумент.ПолучитьТекст();
		Шаблон = СтрЗаменить(Шаблон, "\n", Символы.ПС);
	КонецЕсли;

	Сервер = "https://infostart.ru";
	Адрес  = "/profile/" + Пользователь + "/";
	Если ЗначениеЗаполнено(Фильтр) Тогда
		Адрес = Адрес + "public/?sort=";
		Если Фильтр = "Top" Тогда
			Адрес = Адрес + "property_rating";
		ИначеЕсли Фильтр = "Download" Тогда
			Адрес = Адрес + "property_count_download";
		ИначеЕсли Фильтр = "Sale" Тогда
			Адрес = Адрес + "property_sale_sum";
		ИначеЕсли Фильтр = "Comment" Тогда
			Адрес = Адрес + "property_comments";
		ИначеЕсли Фильтр = "Show" Тогда
			Адрес = Адрес + "show_counter";
		Иначе
			Сообщить("Фильтр не распознан. Будут показаны лучшие публикации");
			Адрес = Адрес + "property_rating";
		КонецЕсли;
	Иначе
		// Если фильтр очищен, то выводим последние публикации для совместимости
		Адрес = Адрес + "public/?sort=active_from";
	КонецЕсли;

	НовыйЗапрос = Новый HTTPЗапрос(Адрес);
	Соединение  = Новый HTTPСоединение(Сервер, 443, , , , 300);
	Ответ       = Соединение.ВызватьHTTPМетод("GET", НовыйЗапрос);

	HTML = Ответ.ПолучитьТелоКакСтроку();
	HTML = Прав(HTML, СтрДлина(HTML) - СтрНайти(HTML, "grid-boxes container-fluid"));
	HTML = Лев(HTML , СтрНайти(HTML, "clearfix pagination-wrap"));

	МассивДанных      = Новый Массив;
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.УстановитьТекст(HTML);

	СоответствиеПризнаков = Новый Соответствие;
	СоответствиеПризнаков.Вставить("<a class=""font-md"" href=", 0);
	СоответствиеПризнаков.Вставить("<a href=", 0);
	СоответствиеПризнаков.Вставить("obj-rate-count-p", 0);
	СоответствиеПризнаков.Вставить("<p style=""overflow: hidden;text-overflow: ellipsis;"" class="" public-preview-text-wrap"">", 1);
	СоответствиеПризнаков.Вставить("/upload/iblock/", 0);
	СоответствиеПризнаков.Вставить("fa fa-calendar", 0);
	СоответствиеПризнаков.Вставить("fa fa-eye", 0);
	СоответствиеПризнаков.Вставить("fa-comments", 0);

	Для Индекс = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Индекс);

		Для Каждого Признак Из СоответствиеПризнаков Цикл
			Если СтрНайти(ТекущаяСтрока, Признак.Ключ) > 0 Тогда

				СтрокаДанных = ТекстовыйДокумент.ПолучитьСтроку(Индекс + Признак.Значение);
				Путь         = ДостатьПути(СтрокаДанных);
				УдалитьТеги(СтрокаДанных);

				Если ЗначениеЗаполнено(СтрокаДанных) И СтрНайти(СтрокаДанных, "&nbsp;") = 0 Тогда
					МассивДанных.Добавить(СтрокаДанных);
					Сообщить(СтрокаДанных);
				КонецЕсли;

				Если ЗначениеЗаполнено(Путь) Тогда
					МассивДанных.Добавить(Путь);
					Сообщить(Путь);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

	КонецЦикла;

	МассивСтруктурДанных = МассивСтруктурДанных(МассивДанных);
	СформироватьИтоговыйТекст(МассивСтруктурДанных);

КонецПроцедуры

Функция ДостатьПути(Данные)

	Путь = "";

	Если СтрНайти(Данные, "data-src") > 0 Тогда
		Путь = СтрЗаменить(Данные, "data-src='", "");
		Путь = СтрЗаменить(Путь, "'></a>", "");
		Путь = СтрЗаменить(Путь, "'>", "");
		Путь = СокрЛП(Путь) + "?" + Строка(Новый УникальныйИдентификатор);
		Данные = "";
	ИначеЕсли СтрНайти(Данные, "href=") > 0 Тогда

		Путь = Прав(Данные, СтрДлина(Данные) - СтрНайти(Данные, "/"));
		Путь = Лев(Путь, СтрНайти(Путь, "/"" "));
		Путь = "/" + Путь;
	
	Иначе 
		Возврат Путь;
	КонецЕсли;

	Путь = "https://infostart.ru" + СокрЛП(Путь);
	Возврат Путь;

КонецФункции

Процедура УдалитьТеги(Данные)

	СтрокаУдаления = "";
	Удалять        = СтрНайти(Данные, ">") < СтрНайти(Данные, "<");
	МассивУдаления = Новый Массив;

	Для Н = 1 По СтрДлина(Данные) Цикл

		ТекущийСимвол = Сред(Данные, Н, 1);

		Если ТекущийСимвол = "<" Или ТекущийСимвол = ">" Тогда
			Удалять = Не Удалять;

			Если Не Удалять И ЗначениеЗаполнено(СтрокаУдаления) Тогда
				СтрокаУдаления = СтрокаУдаления + ТекущийСимвол;
				МассивУдаления.Добавить(СтрокаУдаления);
				СтрокаУдаления = "";
			КонецЕсли;

		КонецЕсли;

		Если Удалять Тогда
			СтрокаУдаления = СтрокаУдаления + ТекущийСимвол;
		КонецЕсли;

	КонецЦикла;

	Если ЗначениеЗаполнено(СтрокаУдаления) Тогда
		МассивУдаления.Добавить(СтрокаУдаления);
	КонецЕсли;
	
	Для Каждого ЭлементУдаления Из МассивУдаления Цикл
		Данные = СтрЗаменить(Данные, ЭлементУдаления, "");
	КонецЦикла;

	Данные = СтрЗаменить(Данные, "</a>", "");
	Данные = СтрЗаменить(Данные, "</span>", "");
	Данные = СокрЛП(Данные);
	
КонецПроцедуры

Функция МассивСтруктурДанных(Знач МассивДанных)

	// При сборе данных с фильтром информация идет в таком порядке:
 	СоответствиеДанных = Новый Соответствие();
	СоответствиеДанных.Вставить(0, "АдресИзображения");
	СоответствиеДанных.Вставить(1, "Описание");
	СоответствиеДанных.Вставить(2, "Звезды");
	СоответствиеДанных.Вставить(3, "Комментарии");
	СоответствиеДанных.Вставить(4, "Дата");
	СоответствиеДанных.Вставить(5, "Заголовок");
	СоответствиеДанных.Вставить(6, "Ссылка");
	СоответствиеДанных.Вставить(7, "Комментарии"); // Дважды возвращаются комментарии, так на сайте Инфостарт
	СоответствиеДанных.Вставить(8, "Просмотры");

	КоличествоПараметров = СоответствиеДанных.Количество();
	ВсегоСтрок = МассивДанных.Количество() / КоличествоПараметров;
	ВсегоСтрок = Мин(Мин(ВсегоСтрок, Число(КоличествоСтатей)), МаксимальноеКоличествоПубликаций);
	
	// Сложим данные в массив структур, чтобы к ним было проще обращаться в дальнейшем
	МассивСтруктурДанных = Новый Массив;
	Для Н = 0 По ВсегоСтрок - 1 Цикл
		СтруктураДанных = Новый Структура;
		Для Каждого КлючИЗначение Из СоответствиеДанных Цикл
			СтруктураДанных.Вставить(КлючИЗначение.Значение, МассивДанных[КлючИЗначение.Ключ + Н * КоличествоПараметров]);
		КонецЦикла;
		МассивСтруктурДанных.Добавить(СтруктураДанных);
	КонецЦикла;

	Возврат МассивСтруктурДанных;

КонецФункции

Процедура СформироватьИтоговыйТекст(Знач МассивСтруктурДанных)
	
	ВыходнойТекст = "";
	// Соответствие параметров описано в шаблоне:
	СоответствиеДанных = Новый Соответствие();
	СоответствиеДанных.Вставить("1", "АдресИзображения");
	СоответствиеДанных.Вставить("2", "Заголовок");
	СоответствиеДанных.Вставить("3", "Ссылка");
	СоответствиеДанных.Вставить("4", "Звезды");
	СоответствиеДанных.Вставить("5", "Описание");
	СоответствиеДанных.Вставить("6", "Дата");
	СоответствиеДанных.Вставить("7", "Комментарии");
	СоответствиеДанных.Вставить("8", "Просмотры");
	Для Каждого СтруктураДанных Из МассивСтруктурДанных Цикл
		ТекущийШаблон = Шаблон;
		Для Каждого КлючИЗначение Из СоответствиеДанных Цикл
			ТекущийШаблон = СтрЗаменить(
				ТекущийШаблон, 
				"%" + КлючИЗначение.Ключ,
				СтруктураДанных[КлючИЗначение.Значение]);
		КонецЦикла;
		ВыходнойТекст = ВыходнойТекст + ТекущийШаблон;
	КонецЦикла;

	ЗаписатьИзменениеВReadme(ВыходнойТекст);

КонецПроцедуры

Процедура ЗаписатьИзменениеВReadme(ВыходнойТекст)

	Readme        = Новый Файл(ПутьКФайлуReadme);
	НомерСтроки   = 0;
	МассивОчистки = Новый Массив;

	Если Не Readme.Существует() Тогда
		Сообщить("Не найден файл Readme!", СтатусСообщения.ОченьВажное);
		Возврат;
	КонецЕсли;

	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ПутьКФайлуReadme);

	Для Индекс = 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл
		
		ТекущаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Индекс);

		Если СтрНайти(ТекущаяСтрока, "<div id=""infostart_posts""") > 0 Тогда
			НомерСтроки = Индекс;
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Если НомерСтроки = 0 Тогда
		Сообщить("Не найден тэг постов!", СтатусСообщения.ОченьВажное);
		Возврат;
	КонецЕсли;

	Для Индекс = НомерСтроки + 1 По ТекстовыйДокумент.КоличествоСтрок() Цикл

		ТекущаяСтрока = ТекстовыйДокумент.ПолучитьСтроку(Индекс);

		Если СтрНайти(ТекущаяСтрока, "</div>") > 0 Или СтрНайти(ТекущаяСтрока, "</ div>") > 0 Тогда
			Прервать;
		КонецЕсли;

		МассивОчистки.Добавить(Индекс);

	КонецЦикла;

	Для Н = 0 По МассивОчистки.ВГраница() Цикл
		ТекстовыйДокумент.УдалитьСтроку(МассивОчистки[МассивОчистки.ВГраница() - Н]);
	КонецЦикла;

	ТекстовыйДокумент.ВставитьСтроку(НомерСтроки + 1, "");

	Сообщить(ТекстовыйДокумент.ПолучитьТекст());

	ТекстовыйДокумент.ВставитьСтроку(НомерСтроки + 2, ВыходнойТекст);
	ТекстовыйДокумент.Записать(ПутьКФайлуReadme);

КонецПроцедуры

Попытка
	ОсновнаяОбработка();	
Исключение
	ОО = ОписаниеОшибки();
	Сообщить(ОО, СтатусСообщения.ОченьВажное);
КонецПопытки;
